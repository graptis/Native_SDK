import org.apache.tools.ant.taskdefs.condition.Os
	
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

File pvrSdkDir = file("${project.buildDir}/../../../../../../../")

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.powervr.VulkanNavigation2D"
        minSdkVersion 16
        targetSdkVersion 24
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext{
                ndkArgs=['-j8']
            }

        }
        debug {
            jniDebuggable true
            ext {
                ndkArgs = ['-j8', 'DEBUG=1', 'NDK_DEBUG=1']
            }
        }
    }
    if (System.env.APP_ABI) {
        buildTypes.release.ndkArgs << ' APP_ABI='+System.env.APP_ABI + ''
        buildTypes.debug.ndkArgs << ' APP_ABI='+System.env.APP_ABI + ''
    }
	
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jni.srcDirs= [] //Disable built-in ndk building.
            jniLibs.srcDir 'libs'

        }
    }

//Copy all our asset files into the android assets folder.
//Add to this list files that will need to be read at runtime here: Textures, shaders, text files etc.
FileCollection assetFiles = files(
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/ATM.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/bank.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/bar.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/butcher.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/cafe.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/casino.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/cinema.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/college.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/courthouse.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/dentist.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/doctors.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/embassy.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/fastfood.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/fire.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/florist.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/font.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/hairdresser.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/hospital.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/library.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/optician.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/other.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/parking.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/petrol.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/pharmacy.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/police.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/postbox.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/postoffice.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/pub.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/restaurant.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/Road.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/shop.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/theatre.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/toilets.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/university.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/vets.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/worship.pvr",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/AA_VertShader_vk.vsh",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/AA_VertShader_vk.vsh.spv",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/AA_FragShader_vk.fsh",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/AA_FragShader_vk.fsh.spv",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/VertShader_vk.vsh",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/VertShader_vk.vsh.spv",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/FragShader_vk.fsh",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/Vulkan/FragShader_vk.fsh.spv",
"${pvrSdkDir}/Examples/Navigation/Navigation2D/ProcessedAssets/map.osm",

)

    String ndkBuildCommand = 'ndk-build';

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ndkBuildCommand = 'ndk-build.cmd'
    }

	//Runs before ndk building tasks
    task copyAssets(type: Copy){
        from assetFiles
        into 'assets'
    }

	//Main tasks for ndk Building of the application. Dependencies will be added to framework projects later
    task ndkBuildDebug(type: Exec, dependsOn:[copyAssets]) {
        executable ndkBuildCommand
		args buildTypes.debug.ndkArgs
    }

    task ndkBuildRelease(type: Exec, dependsOn:[copyAssets]) {
        executable ndkBuildCommand
		args buildTypes.release.ndkArgs
    }

    task ndkBuildClean(type: Exec) {
        executable ndkBuildCommand
		args buildTypes.release.ndkArgs + 'clean'
    }

    //In order to shorten this file a bit, create the NDK Build tasks dynamically for each Framework project.
    // We will be creating a debug and a release task for each PVR Framework project dependency.
	// Otherwise we would need to add 3 tasks per project... (build debug, build release, clean...)

    def ndkDependencies=[
    ["PVRCore", "${pvrSdkDir}/Framework/PVRCore/Build/Android"],
    ["PVRAssets", "${pvrSdkDir}/Framework/PVRAssets/Build/Android"],
    ["PVRVk", "${pvrSdkDir}/Framework/PVRVk/Build/Android"],
    ["PVRShell", "${pvrSdkDir}/Framework/PVRShell/Build/Android"],
    ["PVRUtilsVk", "${pvrSdkDir}/Framework/PVRUtils/Vulkan/Build/Android"]
    ]

	//Create and configure a task for each combination of build type (debug/release or clean) and dependency
	//For example, there will be task for ndkBuildPVRAssetsDebug, ndkBuildPVRAssetsRelease, ndkBuildPVRAssetsClean
    android.buildTypes.all{ theBuildType -> //Debug/Release
        ndkDependencies.each() { 
            project -> //The list above ProjectName, ProjectPath
            String projectName = "ndkBuild${project[0]}${theBuildType.name.capitalize()}" //e.g. ndkBuildPVRCoreDebug
            task(projectName, type: Exec) {
                workingDir "${project[1]}"
                executable ndkBuildCommand
                args theBuildType.ext.ndkArgs
            }
            task(projectName + "Clean", type: Exec) { //e.g. ndkBuildPVRCoreClean
                workingDir "${project[1]}"
                executable ndkBuildCommand
                args theBuildType.ext.ndkArgs + 'clean'
            }
            tasks.findByName("ndkBuild${theBuildType.name.capitalize()}").dependsOn projectName
			clean.dependsOn projectName + "Clean"
        }
    }

	    tasks.withType(JavaCompile) {
        theTask ->
            buildTypes.each() { theBuildType ->
                if(theTask.name.contains("compile${theBuildType.name.capitalize()}Java")){
                    theTask.dependsOn "ndkBuild${theBuildType.name.capitalize()}"
                }
            }
    }

	//Copy outputs to the bin/ folder. Less idiomatic, more understandable...
	task copyToBin(type: Copy) {
        from 'build/outputs/apk/'
        into 'bin/'
        include('*.apk')
   }

    assembleRelease {
        doLast() {
            copyToBin.execute()
        }
    }

    clean.dependsOn ndkBuildClean
    clean {
        doLast() {
            delete "assets"
            delete "obj"
            delete "bin"
        }
    }

    tasks.withType(Exec) {
        doFirst {
            println commandLine
        }
    }
    preBuild.dependsOn copyAssets
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
